import org.apache.tools.ant.taskdefs.condition.Os

plugins {
	id "application"
	id "com.gradleup.shadow" version "9.3.1"
	id "org.flywaydb.flyway" version "11.20.0"
	id "org.jooq.jooq-codegen-gradle" version "3.20.10"
}

final def reactAppPath = new File(this.rootProject.rootDir, "react-app").toPath()

dependencies {
	
	//javalin
	implementation "io.javalin:javalin-bundle:6.7.0"
	
	//logging
	implementation "org.slf4j:slf4j-api:2.0.17"
	implementation "ch.qos.logback:logback-core:1.5.27"
	implementation "ch.qos.logback:logback-classic:1.5.27"
	
	//reactor
	implementation "io.projectreactor:reactor-core:3.8.2"
	
	//jooq for database connection
	implementation "org.jooq:jooq:3.20.11"
	
	//mysql database driver
	implementation "com.mysql:mysql-connector-j:9.6.0"
	
	//password hashing
	implementation "com.password4j:password4j:1.8.4"
	
}

application {
	mainClass.set("com.resonance.server.Server")
}

//set working directory to 'run'
this.tasks.withType(JavaExec).configureEach {
	println("RUN: " + it.name + " " + it.class)
	
	workingDir = file("run")
	doFirst {
		file("run").mkdirs()
	}
}

this.tasks.register("buildWeb", Exec) {
	shouldRunAfter(classes)
	
	workingDir reactAppPath
	
	final String npmCommand = Os.isFamily(Os.FAMILY_WINDOWS) ? 'npm.cmd' : 'npm'
	
	commandLine npmCommand, 'install'
	commandLine npmCommand, 'run', 'build'
	
	
	// Optional: Define inputs and outputs for incremental builds
	inputs.files fileTree(reactAppPath.resolve("src"))
	inputs.file reactAppPath.resolve("package.json")
	inputs.file reactAppPath.resolve("tailwind.config.js")
	inputs.file reactAppPath.resolve("postcss.config.js")
	outputs.dir reactAppPath.resolve("dist")
}

shadowJar {
	dependsOn(buildWeb)
	
	into("public")  {
		from(buildWeb)
	}
}

this.build.dependsOn(this.shadowJar)